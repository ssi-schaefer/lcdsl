module com.wamas.ide.launching.GenerateLcDsl

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."
var baseName = "com.wamas.ide.launching"
var java_source = "src/main/java"
var xtext_source = "src/gen/xtext"

Workflow {
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = baseName
				rootPath = rootPath
				runtime = {
					src = "${rootPath}/${baseName}/${java_source}"
					srcGen = "${rootPath}/${baseName}/${xtext_source}"
				}
				genericIde = {
					src = "${rootPath}/${baseName}.ide/${java_source}"
					srcGen = "${rootPath}/${baseName}.ide/${xtext_source}"
				}
				eclipsePlugin = {
					enabled = true
					src = "${rootPath}/${baseName}.ui/${java_source}"
					srcGen = "${rootPath}/${baseName}.ui/${xtext_source}"
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\n"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
			}
		}
		language = StandardLanguage {
			name = "com.wamas.ide.launching.LcDsl"
			fileExtensions = "lc"

			serializer = {
				generateStub = false
			}
			formatter = {
				generateStub = true
			}
			validator = {
				composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
			}
			parserGenerator = {
				antlrParam = "-Xmaxinlinedfastates"
				antlrParam = "30"
			}
		}
	}
}
